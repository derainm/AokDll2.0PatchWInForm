/* ************ NOMAD ************ */
/* 5 APR 99 */

#include_drs random_map.def 54000
ai_info_map_type        COASTAL

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>

base_terrain WATER

start_random
  percent_chance 20
  #define DESERT_MAP
  percent_chance 20
  #define ALPINE_MAP
end_random

create_player_lands 
{ 
if DESERT_MAP
  terrain_type                     DESERT
elseif ALPINE_MAP
  terrain_type                     GRASS2
else           
  terrain_type                     GRASS
endif
  land_percent                     60
  base_size                        8
start_random
  percent_chance 50
    left_border                    8
  percent_chance 50
    right_border                   8
end_random
start_random
  percent_chance 50
    top_border                     8
  percent_chance 50
    bottom_border                  8
end_random
border_fuzziness                 15
zone                             1
other_zone_avoidance_distance    7
clumping_factor                 15
}

/* ****************************************************** */
<TERRAIN_GENERATION>

create_terrain DEEP_WATER
{
base_terrain WATER
number_of_clumps               3
spacing_to_other_terrain_types 5
land_percent 20
}

/* PRIMARY FOREST */

if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DESERT
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if DESERT_MAP
   create_terrain GRASS
{
  base_terrain                   DESERT
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   6
  set_scale_by_size
}
else
create_terrain DESERT
{
  base_terrain                   GRASS
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if DESERT_MAP
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ALPINE_MAP
create_terrain FOREST
{
  base_terrain                   GRASS3
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   DESERT
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if DESERT_MAP
   create_terrain DIRT3
{
  base_terrain                   DESERT
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if DESERT_MAP
   create_terrain GRASS3
{
  base_terrain                   DESERT
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif ALPINE_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* ****************************************************** */
<OBJECTS_GENERATION>

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       25
 }

create_object RELIC
{
   set_gaia_object_only
   set_place_for_every_player
 }

/* SPECIAL STUFF FOR REGICIDE */

if REGICIDE
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       25
}
endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  6
}

/* BAA BAA */

create_object SHEEP
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_group_placement  5
}

create_object SHEEP
{
   number_of_objects 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_group_placement  5
}

create_object SHEEP
{
   number_of_objects 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_group_placement  5
}

create_object DEER
{
  number_of_objects          6
  group_variance             2
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  4
}

create_object BOAR
{
  number_of_objects          2
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object BOAR
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  11
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          6
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement  7
}

/* RESOURCES BY NUMBER OF PLAYERS */

create_object STONE
{
  number_of_groups           1    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_scaling_to_player_number
  min_distance_group_placement  7
}

create_object GOLD
{
  number_of_groups           1    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_scaling_to_player_number
  min_distance_group_placement  7
}

/* RESOURCES BY MAP */

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           1    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

create_object HAWK
{
   number_of_objects         6
   set_scaling_to_map_size
}

create_object WOLF
{
  number_of_objects          1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_to_players    40
  max_distance_to_players    120
} 

/* BALANCING FISH - WAS 40 */

create_object FISH
{
  number_of_objects                   15
  set_scaling_to_map_size
  terrain_to_place_on                WATER
  set_gaia_object_only
} 

create_object FISH
{
  number_of_objects                   10
  set_scaling_to_map_size
  terrain_to_place_on                DEEP_WATER
  set_gaia_object_only
} 

create_object SHORE_FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  terrain_to_place_on                WATER
  min_distance_group_placement         3
  set_gaia_object_only
}

create_object DOLPHIN
{
  number_of_groups                      10
  number_of_objects                     1
  set_scaling_to_map_size
  terrain_to_place_on                 DEEP_WATER
  set_gaia_object_only
  min_distance_group_placement        10 
} 

create_object DEER
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
}
elseif ALPINE_MAP
create_object PINETREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
}
else
create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
}
endif

/* ****************************************************** */

<ELEVATION_GENERATION>

#include std_elevation.inc

/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land
{
/* replace_terrain GRASS         DESERT */
  replace_terrain WATER         SHALLOW
  replace_terrain DEEP_WATER         SHALLOW
/* replace_terrain FOREST        DESERT  
  replace_terrain PALM_DESERT   DESERT  */
  terrain_cost    WATER         7
  terrain_cost    DEEP_WATER    15
  terrain_cost    FOREST        7
  terrain_cost    PINE_FOREST   7
  terrain_cost    PALM_DESERT   7
  terrain_cost    SHALLOW       3
  terrain_cost    DESERT        1
  terrain_cost    GRASS         2
  terrain_cost    BEACH         7
  terrain_size    WATER         2       1
  terrain_size    DEEP_WATER    2       1  
  terrain_size    GRASS         0       0
  terrain_size    FOREST        0       0
  terrain_size    PINE_FOREST   0       0
  terrain_size    PALM_DESERT   0       0
  terrain_size    DESERT        0       0
}


