/* ************ ECL VALLEY ************ */
/*
 * Valley map for the ECL. This map is used in the qualifiers and in the
 * Eastern Europe region.
 * We use only 2 of the 5 possible biomes for inclusion in the Eastern Europe
 * region.
 * Note, if you wish to use this map for yourself, there was a bug in the
 * snow/ice version of the map called RAMPOUCH. The deer should be restricted
 * to the central valley, but instead the snow grass terrain is available across
 * the entire map, not just the valley. I changed two instances of GRASS_SNOW
 * to DIRT_SNOW to keep the dirt in the middle.
 */
#include_drs random_map.def 54000


/* Note: random_map.def is automatically included in every rms script */



/* ************************************************************************** */
<PLAYER_SETUP>
random_placement



/* ************************************************************************** */
<LAND_GENERATION>
  base_terrain DIRT3

create_player_lands
{

  terrain_type GRASS
  base_size 20
  land_percent 100
  set_zone_by_team
  other_zone_avoidance_distance 10
}


/* ************************************************************************** */
<ELEVATION_GENERATION>

create_elevation 4
{
  base_terrain GRASS
  number_of_clumps    15
  number_of_tiles     2200
  set_scale_by_groups
  set_scale_by_size
}


/* ************************************************************************** */
<TERRAIN_GENERATION>

create_terrain BEACH
{
  base_terrain DIRT3
  land_percent 100
  number_of_clumps 100
  spacing_to_other_terrain_types 2
}

create_terrain BEACH
{
  base_terrain DIRT3
  land_percent 100
  number_of_clumps 100
  spacing_to_other_terrain_types 2
}
/*
create_terrain ICE
{
  base_terrain GRASS_SNOW
  land_percent 100
  number_of_clumps 100
  spacing_to_other_terrain_types 2
}

create_terrain ICE
{
  base_terrain GRASS_SNOW
  land_percent 100
  number_of_clumps 100
  spacing_to_other_terrain_types 2
}

*/
create_terrain WATER
{
    base_terrain BEACH
  if TINY_MAP
  number_of_tiles 80
  elseif MEDIUM_MAP
  number_of_tiles 120
  else
  number_of_tiles 160
  endif
  number_of_clumps 10
}



create_terrain FOREST
{
  base_terrain GRASS
  spacing_to_other_terrain_types 5
  land_percent 12
  number_of_clumps 20
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain GRASS3
{
  base_terrain GRASS
  number_of_clumps 80
  spacing_to_other_terrain_types 0
  start_random
  percent_chance 50
  land_percent 20
  percent_chance 50
  land_percent 16
  end_random
  set_scale_by_size
}


create_terrain GRASS
{
  base_terrain GRASS3
  number_of_clumps 50
  spacing_to_other_terrain_types 0
  start_random
  percent_chance 50
  land_percent 12
  percent_chance 50
  land_percent 8
  end_random
  set_scale_by_size
}


/* ************************************************************************** */
<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
     set_place_for_every_player
     group_placement_radius 18
     min_distance_to_players 0
     max_distance_to_players 0
}

create_object VILLAGER
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

if REGICIDE

create_object VILLAGER
{
     number_of_objects 7
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

endif

create_object SCOUT
{
     number_of_objects 1
     set_place_for_every_player
     min_distance_to_players 7
     max_distance_to_players 9
}

if REGICIDE

create_object KING
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

create_object CASTLE
{
     set_place_for_every_player
     min_distance_to_players 9
     max_distance_to_players 12
}

endif


create_object SHEEP
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}


create_object SHEEP
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 24
}

create_object DEER
{
  start_random
  percent_chance 50
  number_of_objects 4
  percent_chance 50
  number_of_objects 3
  end_random
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  start_random
  percent_chance 50
  min_distance_to_players 14
  max_distance_to_players 18
  percent_chance 50
  min_distance_to_players 25
  max_distance_to_players 28
  end_random
}


create_object BOAR
{
     number_of_objects          1
     number_of_groups     1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}

create_object BOAR
{
     number_of_objects          1
     number_of_groups     1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}

create_object GOLD
{
     number_of_objects 5
     number_of_groups 2
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 18
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object GOLD
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 19
     max_distance_to_players 25
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 2
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 18
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 23
     max_distance_to_players 28
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 13
  min_distance_group_placement 6
}


create_object OAKTREE
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
}


create_object OAKTREE
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
}

/* Far gold & stone */

create_object GOLD
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          4
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          4
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          5
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          5
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          6
else
     number_of_groups           24
     number_of_objects          6
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object STONE
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          3
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          3
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          4
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          4
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          4
else
     number_of_groups           24
     number_of_objects          4
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}


create_object RELIC
{
     number_of_objects 5
      min_distance_to_players 25
     temp_min_distance_group_placement 20
}

create_object WOLF
{
     number_of_groups 3
     set_loose_grouping
        number_of_objects 1
  set_scaling_to_map_size
     set_gaia_object_only
     min_distance_group_placement 12
     min_distance_to_players 40
}

create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}

create_object DEER
{
  number_of_objects 4
  number_of_groups 100
  set_gaia_object_only
  terrain_to_place_on DIRT3
  min_distance_group_placement 10
}

create_object SHORE_FISH
{
  number_of_objects                   25
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement  3
}
